group 'net.benwoodworth'

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "com.github.johnrengelman.shadow"

buildscript {
    PluginVersion.updateVersion()
    version = PluginVersion.version

    ext {
        plugin_author = "Kepler"
        plugin_description = "Redefines crafting in Minecraft"
        plugin_website = "https://github.com/BenWoodworth/FastCraft"

        version_java = "1.8"
        version_kotlin = "1.3.61"
        version_dagger = "2.25.2"
        version_javax_inject = "1"
        version_auto_factory = "1.0-beta7"
        version_shadow = "5.2.0"
        version_bstats = "1.3"
        version_bukkit = "1.13-R0.1-SNAPSHOT"

        dependency_kotlin_stdlib = "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
        dependency_kotlin_stdlib_jdk8 = "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
        dependency_dagger = "com.google.dagger:dagger:$version_dagger"
        dependency_dagger_compiler = "com.google.dagger:dagger-compiler:$version_dagger"
        dependency_javax_inject = "javax.inject:javax.inject:$version_javax_inject"
        dependency_auto_factory = "com.google.auto.factory:auto-factory:$version_auto_factory"
        dependency_bstats_bukkit = "org.bstats:bstats-bukkit:$version_bstats"
        dependency_bukkit = "org.bukkit:bukkit:$version_bukkit"
    }

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "com.github.jengelman.gradle.plugins:shadow:$version_shadow"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

sourceSets {
    common {
        java.srcDirs += "$buildDir/generated/source/kapt/common"
    }

    bukkit {
        java.srcDirs += "$buildDir/generated/source/kapt/bukkit"
        compileClasspath += common.compileClasspath
        runtimeClasspath += common.runtimeClasspath
    }
}

dependencies {
    def allImplementation = { dependency ->
        implementation dependency
        commonImplementation dependency
        bukkitImplementation dependency
    }

    def kaptAll = { dependency ->
        kapt dependency
        kaptCommon dependency
        kaptBukkit dependency
    }

    allImplementation dependency_kotlin_stdlib
    allImplementation dependency_kotlin_stdlib_jdk8
    allImplementation dependency_dagger
    allImplementation dependency_javax_inject
    allImplementation dependency_auto_factory

    kaptAll dependency_dagger_compiler
    kaptAll dependency_auto_factory

    bukkitImplementation sourceSets.common.output
    bukkitImplementation dependency_bukkit
}

compileKotlin.kotlinOptions.jvmTarget = version_java
compileTestKotlin.kotlinOptions.jvmTarget = version_java

kapt {
    correctErrorTypes = true
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    archiveFileName = "FastCraft.jar"

    from sourceSets.common.output
    from sourceSets.bukkit.output

    dependencies {
        include dependency(dependency_kotlin_stdlib)
        include dependency(dependency_kotlin_stdlib_jdk8)
        include dependency(dependency_dagger)
        include dependency(dependency_javax_inject)
        include dependency(dependency_bstats_bukkit)
    }

    def libPackage = "net.benwoodworth.fastcraft.lib"

    relocate("kotlin", "${libPackage}.kotlin")
    relocate("dagger", "${libPackage}.dagger")
    relocate("javax", "${libPackage}.javax")
    relocate("org.bstats", "${libPackage}.bstats")
}

processBukkitResources {
    include 'plugin.yml'

    expand([
            plugin_author     : plugin_author,
            plugin_description: plugin_description,
            plugin_version    : version,
            plugin_website    : plugin_website
    ])
}
