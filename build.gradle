group "net.benwoodworth"

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "com.github.johnrengelman.shadow"

buildscript {
    PluginVersion.updateVersion()
    version = PluginVersion.version

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "com.github.jengelman.gradle.plugins:shadow:$version_shadow"
    }
}

repositories {
    jcenter()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
}

sourceSets {
    common {
        java.srcDirs += "$buildDir/generated/source/kapt/common"
    }

    bukkit {
        java.srcDirs += "$buildDir/generated/source/kapt/bukkit"
        compileClasspath += common.compileClasspath
        runtimeClasspath += common.runtimeClasspath
    }
}

dependencies {
    def allImplementation = { dependency ->
        implementation dependency
        commonImplementation dependency
        bukkitImplementation dependency
    }

    def kaptAll = { dependency ->
        kapt dependency
        kaptCommon dependency
        kaptBukkit dependency
    }

    allImplementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
    allImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
    allImplementation "com.google.dagger:dagger:$version_dagger"
    allImplementation "javax.inject:javax.inject:$version_javax_inject"
    allImplementation "com.google.auto.factory:auto-factory:$version_auto_factory"

    kaptAll "com.google.dagger:dagger-compiler:$version_dagger"
    kaptAll "com.google.auto.factory:auto-factory:$version_auto_factory"

    bukkitImplementation sourceSets.common.output
    bukkitImplementation "org.bukkit:bukkit:$version_bukkit"
}

compileKotlin.kotlinOptions.jvmTarget = version_java
compileTestKotlin.kotlinOptions.jvmTarget = version_java

kapt {
    correctErrorTypes = true
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    archiveFileName = "FastCraft.jar"

    from sourceSets.common.output
    from sourceSets.bukkit.output

    dependencies {
        exclude dependency("org.bukkit:bukkit")
    }

    def libPackage = "net.benwoodworth.fastcraft.lib"
    relocate("kotlin", "${libPackage}.kotlin")
    relocate("dagger", "${libPackage}.dagger")
    relocate("javax", "${libPackage}.javax")
    relocate("org.bstats", "${libPackage}.bstats")
}

processBukkitResources {
    include "plugin.yml"

    expand([
            plugin_author     : plugin_author,
            plugin_description: plugin_description,
            plugin_version    : version,
            plugin_website    : plugin_website
    ])
}
