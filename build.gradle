buildscript {
    PluginVersion.updateVersion()
    version = PluginVersion.version

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

task build(group: "build", dependsOn: ":fastcraft:build") {
    doLast {
        copy {
            from("${project(":fastcraft").buildDir}/libs") {
                include "*.jar"
            }

            into project.buildDir
        }
    }
}

task clean(group: "build") {
    doLast {
        delete project.buildDir
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
        implementation "com.google.dagger:dagger:$version_dagger"
        implementation "javax.inject:javax.inject:$version_javax_inject"
        implementation "com.google.auto.factory:auto-factory:$version_auto_factory"
        implementation "net.benwoodworth.localeconfig:localeconfig-api:$version_localeconfig"

        kapt "com.google.dagger:dagger-compiler:$version_dagger"
        kapt "com.google.auto.factory:auto-factory:$version_auto_factory"
    }

    compileKotlin.kotlinOptions.jvmTarget = version_java
    compileTestKotlin.kotlinOptions.jvmTarget = version_java

    kapt {
        correctErrorTypes = true
        includeCompileClasspath = false
    }

    // Let auto factory build incrementally
    configurations.all {
        resolutionStrategy {
            force "com.google.auto.value:auto-value-annotations:1.6.6"
            eachDependency { details ->
                if (details.requested.group == "com.google.auto.value" && details.requested.name == "auto-value") {
                    details.useTarget "com.google.auto.value:auto-value-annotations:1.6.6"
                }
            }
        }
    }
}
