PluginVersion.updateVersion()
version = PluginVersion.version

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "com.github.jengelman.gradle.plugins:shadow:$version_shadow"
    }
}

apply plugin: "com.github.johnrengelman.shadow"

allprojects {
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    repositories {
        jcenter()
        maven { url "https://repo.codemc.org/repository/maven-public" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$version_kotlin"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
        implementation "com.google.dagger:dagger:$version_dagger"
        implementation "javax.inject:javax.inject:$version_javax_inject"
        implementation "com.google.auto.factory:auto-factory:$version_auto_factory"
        implementation "net.benwoodworth.localeconfig:localeconfig-api:$version_localeconfig"
        implementation "org.bstats:bstats-bukkit-lite:$version_bstats"

        kapt "com.google.dagger:dagger-compiler:$version_dagger"
        kapt "com.google.auto.factory:auto-factory:$version_auto_factory"
    }

    compileKotlin.kotlinOptions.jvmTarget = version_java
    compileTestKotlin.kotlinOptions.jvmTarget = version_java

    kapt {
        correctErrorTypes = true
        includeCompileClasspath = false
    }

    // Let auto factory build incrementally
    configurations.all {
        resolutionStrategy {
            force "com.google.auto.value:auto-value-annotations:1.6.6"
            eachDependency { details ->
                if (details.requested.group == "com.google.auto.value" && details.requested.name == "auto-value") {
                    details.useTarget "com.google.auto.value:auto-value-annotations:1.6.6"
                }
            }
        }
    }
}

dependencies {
    implementation project(':fastcraft-core')
    implementation project(':fastcraft-bukkit')
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    archiveFileName = "FastCraft.jar"

    dependencies {
        subprojects.each {
            include dependency(it)
        }

        include dependency("org.jetbrains.kotlin:kotlin-stdlib")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        include dependency("com.google.dagger:dagger")
        include dependency("javax.inject:javax.inject")
        include dependency("net.benwoodworth.localeconfig:localeconfig-api")
        include dependency("org.bstats:bstats-bukkit-lite")
    }

    def libPackage = "net.benwoodworth.fastcraft.lib"
    relocate("kotlin", "${libPackage}.kotlin")
    relocate("dagger", "${libPackage}.dagger")
    relocate("javax", "${libPackage}.javax")
    relocate("net.benwoodworth.localeconfig", "${libPackage}.localeconfig")
    relocate("org.bstats", "${libPackage}.bstats")

    minimize {
        subprojects.each {
            exclude dependency(it)
        }
    }
}
