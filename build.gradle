PluginVersion.updateVersion()
version = PluginVersion.version

buildscript {
    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: "com.github.johnrengelman.shadow"

allprojects {
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://repo.codemc.org/repository/maven-public" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "com.google.dagger:dagger:$dagger_version"
        implementation "javax.inject:javax.inject:$javax_inject_version"
        implementation "com.google.auto.factory:auto-factory:$auto_factory_version"
        implementation "net.benwoodworth.localeconfig:localeconfig-api:$localeconfig_version"
        implementation "org.bstats:bstats-bukkit-lite:$bstats_version"

        kapt "com.google.dagger:dagger-compiler:$dagger_version"
        kapt "com.google.auto.factory:auto-factory:$auto_factory_version"
    }

    compileKotlin.kotlinOptions.jvmTarget = java_version
    compileTestKotlin.kotlinOptions.jvmTarget = java_version

    kapt {
        correctErrorTypes = true
        includeCompileClasspath = false
    }

    // Let auto factory build incrementally
    configurations.all {
        resolutionStrategy {
            force "com.google.auto.value:auto-value-annotations:1.6.6"
            eachDependency { details ->
                if (details.requested.group == "com.google.auto.value" && details.requested.name == "auto-value") {
                    details.useTarget "com.google.auto.value:auto-value-annotations:1.6.6"
                }
            }
        }
    }
}

dependencies {
    implementation project(':fastcraft-core')
    implementation project(':fastcraft-bukkit')
}

jar {
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    archiveFileName = "FastCraft.jar"

    dependencies {
        subprojects.each {
            include dependency(it)
        }

        include dependency("org.jetbrains.kotlin:kotlin-stdlib")
        include dependency("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        include dependency("com.google.dagger:dagger")
        include dependency("javax.inject:javax.inject")
        include dependency("net.benwoodworth.localeconfig:localeconfig-api")
        include dependency("org.bstats:bstats-bukkit-lite")
    }

    def libPackage = "net.benwoodworth.fastcraft.lib"
    relocate("kotlin", "${libPackage}.kotlin")
    relocate("dagger", "${libPackage}.dagger")
    relocate("javax.inject", "${libPackage}.javax.inject")
    relocate("net.benwoodworth.localeconfig", "${libPackage}.localeconfig")
    relocate("org.bstats", "${libPackage}.bstats")

    minimize {
        subprojects.each {
            exclude dependency(it)
        }
    }
}
