apply plugin: "java"
apply plugin: "com.github.johnrengelman.shadow"

buildscript {
    PluginVersion.updateVersion()
    version = PluginVersion.version

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath dependency_kotlin_gradle_plugin
        classpath dependency_shadow
    }
}

dependencies {
    compile project(":fastcraft:common")
    compile project(":fastcraft:platform-bukkit")
    compile project(":fastcraft:platform-sponge")
}

allprojects {
    repositories {
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        jcenter()
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    dependencies {
        compile dependency_kotlin_stdlib

        compile dependency_jsr250
        compile dependency_javax_inject
        compile dependency_dagger
        
        kapt dependency_dagger_compiler
    }

    sourceSets {
        main.java.srcDirs += "$buildDir/generated/source/kapt/main"
    }

    kapt {
        correctErrorTypes = true
    }
}

jar {
    archivesBaseName = "FastCraft"
    
    // Use shadowJar instead of jar
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    classifier(null)

    dependencies {
        subprojects.forEach { subproject ->
            include project(subproject.path)
        }

        include dependency(dependency_kotlin_runtime)
        include dependency(dependency_kotlin_stdlib)
        include dependency(dependency_dagger)
        include dependency(dependency_javax_inject)
        include dependency(dependency_bstats_bukkit)
        include dependency(dependency_bstats_sponge)
    }

    def libPackage = "net.benwoodworth.fastcraft.lib"

    relocate("kotlin", "${libPackage}.kotlin")
    relocate("dagger", "${libPackage}.dagger")
    relocate("javax", "${libPackage}.javax")
    relocate("org.bstats", "${libPackage}.bstats")
}
